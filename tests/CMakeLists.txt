if(M4RI_FOUND AND OPENSSL_FOUND)
  foreach(target IN ITEMS lowmc_str mzd)
    add_executable("${target}_test" "${target}_test.c")
    target_link_libraries("${target}_test" picnic_static ${M4RI_LIBRARY} m OpenSSL::Crypto)
    apply_base_options("${target}_test")
    target_compile_definitions("${target}_test" PRIVATE PICNIC_STATIC)
    if(WITH_SIMD_OPT)
      target_compile_definitions("${target}_test" PRIVATE WITH_OPT)
      if(CC_SUPPORTS_SSE2 AND WITH_SSE2)
        target_compile_definitions("${target}_test" PRIVATE WITH_SSE2)
        if(CC_SUPPORTS_AVX2 AND WITH_AVX2)
          target_compile_definitions("${target}_test" PRIVATE WITH_AVX2)
        endif()
      endif()
      if(CC_SUPPORTS_NEON AND WITH_NEON)
        target_compile_definitions("${target}_test" PRIVATE WITH_NEON)
      endif()
    endif()
    if(MSVC AND USE_STATIC_RUNTIME)
      target_compile_options("${target}_test" PRIVATE "/MT$<$<CONFIG:Debug>:d>")
    endif()
    if(WITH_MUL_M4RI)
      target_compile_definitions("${target}_test" PRIVATE MUL_M4RI)
    endif()
    target_compile_definitions("${target}_test" PRIVATE WITH_M4RI_UTILS)

    add_test(NAME ${target} COMMAND "${target}_test")
  endforeach(target)
endif()

list(APPEND test_static_targets bitstream kdf_shake256 lowmc)
if(NOT MSVC)
  list(APPEND test_targets picnic extended_picnic)
else()
  list(APPEND test_static_targets picnic extended_picnic)
  list(APPEND test_targets)
endif()
foreach(target IN ITEMS ${test_static_targets})
  add_executable("${target}_test" "${target}_test.c")
  target_link_libraries("${target}_test" picnic_static)
  apply_base_options("${target}_test")
  target_compile_definitions("${target}_test" PRIVATE PICNIC_STATIC)
  if(MSVC AND USE_STATIC_RUNTIME)
    target_compile_options("${target}_test" PRIVATE "/MT$<$<CONFIG:Debug>:d>")
  endif()
  add_test(NAME ${target} COMMAND "${target}_test")
endforeach(target)

foreach(target IN ITEMS ${test_targets})
  add_executable("${target}_test" "${target}_test.c")
  target_link_libraries("${target}_test" picnic)
  apply_base_options("${target}_test")
  add_test(NAME ${target} COMMAND "${target}_test")
endforeach(target)

if(NOT WITH_EXTRA_RANDOMNESS AND HAVE_GETLINE)
  add_executable("kats_test" "kats_test.c")
  target_link_libraries("kats_test" picnic)
  apply_base_options("kats_test")
  target_compile_definitions("kats_test" PRIVATE "-DKATDIR=\"${CMAKE_CURRENT_SOURCE_DIR}\"")
  add_test(NAME kats COMMAND "kats_test")
endif()

if(NOT MSVC)
  foreach(target IN ITEMS L1_FS L1_UR L3_FS L3_UR L5_FS L5_UR)
    add_executable("api_${target}_test" "api_test.c")
    apply_base_options("api_${target}_test")
    target_link_libraries("api_${target}_test" "picnic_${target}")
    target_include_directories("api_${target}_test" PRIVATE
      "${CMAKE_CURRENT_SOURCE_DIR}/../${target}")

    add_test(NAME "api_${target}" COMMAND "api_${target}_test")
  endforeach(target)
endif()
