if(M4RI_FOUND)
  foreach(target IN ITEMS lowmc_str mzd)
    add_executable("${target}_test" "${target}_test.c")
    target_link_libraries("${target}_test" picnic_static ${M4RI_LIBRARY} m)
    apply_base_options("${target}_test")
    apply_opt_options("${target}_test")
    target_compile_definitions("${target}_test" PRIVATE PICNIC_STATIC)
    if(MSVC AND USE_STATIC_RUNTIME)
      target_compile_options("${target}_test" PRIVATE "/MT$<$<CONFIG:Debug>:d>")
    endif()
    target_compile_definitions("${target}_test" PRIVATE WITH_M4RI_UTILS)

    add_test(NAME ${target} COMMAND "${target}_test")
  endforeach(target)
endif()

list(APPEND test_static_targets bitstream kdf_shake256 shake lowmc transpose)
if(NOT MSVC)
  list(APPEND test_targets picnic extended_picnic)
else()
  list(APPEND test_static_targets picnic extended_picnic)
  list(APPEND test_targets)
endif()
foreach(target IN ITEMS ${test_static_targets})
  add_executable("${target}_test" "${target}_test.c")
  target_link_libraries("${target}_test" picnic_static)
  apply_base_options("${target}_test")
  apply_opt_options("${target}_test")
  target_compile_definitions("${target}_test" PRIVATE PICNIC_STATIC)
  if(MSVC AND USE_STATIC_RUNTIME)
    target_compile_options("${target}_test" PRIVATE "/MT$<$<CONFIG:Debug>:d>")
  endif()
  if(WITH_LOWMC_M1)
      target_compile_definitions("${target}_test" PRIVATE WITH_LOWMC_M1)
  endif()
  add_test(NAME ${target} COMMAND "${target}_test")
endforeach(target)

foreach(target IN ITEMS ${test_targets})
  add_executable("${target}_test" "${target}_test.c")
  target_link_libraries("${target}_test" picnic)
  apply_base_options("${target}_test")
  add_test(NAME ${target} COMMAND "${target}_test")
endforeach(target)

if(NOT WITH_EXTRA_RANDOMNESS AND HAVE_GETLINE)
  add_executable("kats_test" "kats_test.c")
  target_link_libraries("kats_test" picnic)
  apply_base_options("kats_test")
  target_compile_definitions("kats_test" PRIVATE "-DKATDIR=\"${CMAKE_CURRENT_SOURCE_DIR}\"")
  add_test(NAME kats COMMAND "kats_test")
endif()

if(NOT MSVC)
  foreach(target IN ITEMS picnic_L1_FS picnic_L1_UR picnic_L3_FS picnic_L3_UR picnic_L5_FS picnic_L5_UR
                          picnic2_L1_FS picnic2_L3_FS picnic2_L5_FS)
    add_executable("api_${target}_test" "api_test.c")
    apply_base_options("api_${target}_test")
    target_link_libraries("api_${target}_test" "${target}")
    target_include_directories("api_${target}_test" PRIVATE
      "${CMAKE_CURRENT_SOURCE_DIR}/../${target}")

    add_test(NAME "api_${target}" COMMAND "api_${target}_test")
  endforeach(target)
endif()
